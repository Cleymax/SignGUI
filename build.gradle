    plugins {
    id 'java-library'
    id 'maven'
    id 'maven-publish'
    id 'signing'
    id 'eclipse'
}

group = 'fr.cleymax'
version = '1.0.0'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

compileJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://hub.spigotmc.org/nexus/content/groups/public/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }
    maven { url = 'http://repo.dmulloy2.net/nexus/repository/public/' }
    mavenLocal() // Required for NMS
}

dependencies {
    implementation 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    implementation 'org.spigotmc:spigot:1.16.4-R0.1-SNAPSHOT' // Full Spigot, allows us to use NMS
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand 'version': project.version
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

javadoc {
    doFirst {
        options.links(
                'https://hub.spigotmc.org/javadocs/spigot/',
                'https://docs.oracle.com/javase/8/docs/api/'
        )
        options.setWindowTitle('SignGUI')
        options.setEncoding("UTF-8")
        options.setDocTitle('SignGUI')
        options.setUse(true)
        options.setVersion(true)
        options.setAuthor(true)
    }
    destinationDir = file("./docs/")

    if (JavaVersion.current() >= JavaVersion.VERSION_1_9) {
        options.addBooleanOption('html5', true)
    }
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts { archives javadocJar, sourcesJar }

if (project.hasProperty("signing.keyId") && project.hasProperty("release")) {
    signing {
        sign configurations.archives
    }
}

gradle.taskGraph.whenReady { taskGraph ->
    def console = System.console()
    if (project.hasProperty("release")) {
        project.version = project.version + '-RELEASE'
        console.printf "> Build new release v" + project.version + " !"
    } else {
        project.version = project.version + '-SNAPSHOT'
        console.printf "> Build new snapshot v" + project.version + " !"
    }
}

publishToMavenLocal {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'fr.cleymax'
                artifactId = 'signgui'
                version = '1.0.0'

                from components.java

                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = 'SignGUI'
                    packaging 'jar'
                    description = 'Sign API for your Minecraft Bukkit plugins.'
                    url = 'https://github.com/Cleymax/SignGUI'

                    licenses {
                        license {
                            name = 'GNU General Public License v3.0'
                            url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                        }
                    }

                    developers {
                        developer {
                            id = 'cleymax'
                            name = 'Clément P. (Cleymax)'
                            email = 'cleymaxpro@gmail.com'
                        }
                    }
                }
            }
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }
            if (project.hasProperty("mavenUser") && project.hasProperty("mavenPassword")) {
                repository(url: repositoryUrl) {
                    authentication(userName: mavenUser, password: mavenPassword)
                }
            }

            pom.project {
                name 'signgui'
                packaging 'jar'
                description 'Sign API for your Minecraft Bukkit plugins.'
                url 'https://github.com/Cleymax/SignGUI'

                scm {
                    connection 'scm:git:git://github.com/Cleymax/SignGUI.git'
                    developerConnection 'scm:git:ssh://github.com:Cleymax/SignGUI.git'
                    url 'http://github.com/Cleymax/SignGUI/tree/master'
                }

                issueManagement {
                    system 'GitHub'
                    url 'https://github.com/Cleymax/SignGUI/issues'
                }

                licenses {
                    license {
                        name 'GNU General Public License v3.0'
                        url 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }

                developers {
                    developer {
                        id 'cleymax'
                        name 'Clément P. (Cleymax)'
                        email 'cleymaxpro@gmail.com'
                    }
                }
            }
        }
    }
}